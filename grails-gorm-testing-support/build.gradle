buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.1.1"
    }
}

apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'project-reports'

repositories {
    mavenLocal()
    maven { url "https://repo.grails.org/grails/core" }
}

version "1.0.0.BUILD-SNAPSHOT"
group "org.grails"

apply plugin: 'groovy'
apply plugin: "com.jfrog.artifactory"
apply plugin: 'project-reports'

dependencies {
    compile "org.grails:grails-plugin-domain-class:$grailsVersion"

    compile project(':grails-testing-support')
}

project.ext {
    githubUrl = 'https://github.com/grails/grails-testing-support'
    artifactId = 'grails-gorm-testing-support'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type:Jar) {
    classifier = 'javadoc'
    from groovydoc.outputs
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId project.artifactId
            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST
                    name project.artifactId
                    description project.description
                    url project.githubUrl
                    scm {
                        url project.githubUrl
                        connection "scm:${project.githubUrl}.git"
                        developerConnection "scm:${project.githubUrl}.git"
                    }
                    licenses {
                        license {
                            name 'Apache License, Version 2.0'
                            url 'https://www.apache.org/licenses/LICENSE-2.0'
                            distribution 'repo'
                        }
                    }
                    developers {
                        developer {
                            id 'jeffscottbrown'
                            name 'Jeff Scott Brown'
                            email 'brownj@ociweb.com'
                        }
                    }
                }
            }
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

artifactory {
    contextUrl = 'http://repo.grails.org/grails'
    publish {
        repository {
            repoKey = 'libs-snapshots-local'

            username = System.getenv('GRAILS_CENTRAL_USERNAME') ?: project.properties.get('grailsPluginsUsername')
            password = System.getenv("GRAILS_CENTRAL_PASSWORD") ?: project.properties.get('grailsPluginsPassword')
        }
        defaults {
            publications('maven')
        }
    }
}
