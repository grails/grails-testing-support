buildscript {
    repositories {
        mavenLocal()
        maven { url "https://repo.grails.org/grails/core" }
    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsVersion"
        classpath "org.grails:grails-docs:${project.ext.properties.grailsDocsVersion ?: grailsVersion}"
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4"
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
        classpath 'io.github.groovylang.groovydoc:groovydoc-gradle-plugin:1.0.1'
        classpath "org.grails.plugins:views-gradle:$jsonViewsVersion"
    }
}

repositories {
    mavenLocal()
    maven { url "https://repo.grails.org/grails/core" }
}
version project.projectVersion

ext {
     commonBuild = 'https://raw.githubusercontent.com/grails/grails-common-build/1549696f8d1a6bce3d84db9c4c1bfde61eddf175'
}



subprojects {

    version project.projectVersion

    ext {
        userOrg = "grails"
        isGrailsPlugin = name.startsWith('grails-plugin')
        isBuildSnapshot = version.toString().endsWith("-SNAPSHOT")
    }

    if(isGrailsPlugin) {
        group "org.grails.plugins"
    }
    else {
        group "org.grails"
    }

    repositories {
        mavenLocal()
        maven { url "https://repo.grails.org/grails/core" }
        if(groovyVersion && groovyVersion.endsWith('-SNAPSHOT')) {
            maven {
                name 'JFrog OSS snapshot repo'
                url 'https://oss.jfrog.org/oss-snapshot-local/'
            }
        }
    }

    if(it.name.startsWith("demo")) {
        apply plugin:"org.grails.grails-web"
        apply plugin:"org.grails.plugins.views-json"
    }
    else {
        if(isGrailsPlugin) {
            apply plugin:"org.grails.grails-plugin"
            apply from:"${commonBuild}/common-publishing.gradle"
        }
        else {
            apply from:"${commonBuild}/common-project.gradle"
            apply from:"${commonBuild}/common-publishing.gradle"
        }

        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

    dependencies {
        implementation "com.github.javaparser:javaparser-core:$javaParserVersion"
        compileOnly "javax.servlet:javax.servlet-api:$servletApiVersion"

        testCompile "javax.servlet:javax.servlet-api:$servletApiVersion"
        testCompile "org.codehaus.groovy:groovy-test-junit5:$groovyVersion"
        testCompile "org.junit.jupiter:junit-jupiter-api:5.6.0"
        testCompile "org.junit.platform:junit-platform-runner:1.6.0"
        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.6.0"
    }

    test {
        useJUnitPlatform()
    }
}

apply from:"${commonBuild}/common-docs.gradle"
