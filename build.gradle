buildscript {
    repositories {
        maven { url "https://repo.grails.org/grails/core" }
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsGradlePluginVersion"
        classpath "org.grails:grails-docs:${project.ext.properties.grailsDocsVersion ?: grailsVersion}"
        classpath "org.grails.plugins:views-gradle:$jsonViewsVersion"
        classpath "io.github.gradle-nexus:publish-plugin:2.0.0"
        classpath("org.asciidoctor:asciidoctor-gradle-jvm:4.0.3")
    }
}

repositories {
    maven { url "https://repo.grails.org/grails/core" }
}

group = "org.grails"
version project.projectVersion

ext {
    commonBuild = 'https://raw.githubusercontent.com/grails/grails-common-build/gradle7'
}

subprojects { project ->

    version rootProject.projectVersion

    ext['groovyVersion'] = System.getenv('CI_GROOVY_VERSION') ?: project.groovyVersion

    configurations.configureEach {

        // FORCE UPGRADE OF GROOVY IN DEPENDENCIES TO GROOVY 4
        resolutionStrategy.eachDependency { DependencyResolveDetails details ->
            if (details.requested.group == 'org.codehaus.groovy') {
                details.useTarget(group: 'org.apache.groovy', name: details.requested.name, version: groovyVersion)
            }
        }
    }

    ext {
        userOrg = "grails"
        isGrailsPlugin = name.startsWith('grails-plugin')
        isBuildSnapshot = version.toString().endsWith("-SNAPSHOT")
    }

    if (isGrailsPlugin) {
        group "org.grails.plugins"
    } else {
        group "org.grails"
    }

    repositories {
        maven { url "https://repo.grails.org/grails/core" }
        if (groovyVersion && groovyVersion.endsWith('-SNAPSHOT')) {
            maven {
                name 'JFrog OSS snapshot repo'
                url 'https://oss.jfrog.org/oss-snapshot-local/'
            }
        }
    }

    apply plugin: "groovy"
    if (project.name.startsWith("examples")) {
        apply plugin: "org.grails.grails-web"
        apply plugin: "org.grails.plugins.views-json"
    } else {
        apply plugin: "java-library"
        if (isGrailsPlugin) {
            apply plugin: "org.grails.grails-plugin"
        } else {
            apply from: "${commonBuild}/common-project.gradle"
        }

        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    dependencies {
        implementation "com.github.javaparser:javaparser-core:$javaParserVersion"
        compileOnly "jakarta.servlet:jakarta.servlet-api:$servletApiVersion"

        testImplementation "jakarta.servlet:jakarta.servlet-api:$servletApiVersion"
        testImplementation "org.apache.groovy:groovy-test-junit5:$groovyVersion"
        testImplementation "org.junit.jupiter:junit-jupiter-api:$junitJupiterVersion"
        testImplementation "org.junit.platform:junit-platform-runner:$junitPlatformVersion"
        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitJupiterVersion"
    }

    tasks.withType(Test) {
        useJUnitPlatform()
    }
}

apply from: "${commonBuild}/common-publishing.gradle"
apply from: "${commonBuild}/common-docs.gradle"
